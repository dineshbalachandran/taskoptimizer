package com.infosys.taskoptimizer.taskassignschedule.solver;
    dialect "java"

import java.util.List;
import java.util.ArrayList;

import org.optaplanner.core.api.score.buildin.hardsoftlong.HardSoftLongScoreHolder;

import com.infosys.taskoptimizer.taskassignschedule.domain.TaskAssignment;
import com.infosys.taskoptimizer.taskassignschedule.domain.Task;
import com.infosys.taskoptimizer.taskassignschedule.domain.Technician;
import com.infosys.taskoptimizer.taskassignschedule.domain.OptControlParameters;
import com.infosys.taskoptimizer.taskassignschedule.drools.PeriodWrapper;
import com.infosys.taskoptimizer.taskassignschedule.drools.TechnicianPeriod;
import com.infosys.taskoptimizer.taskassignschedule.TaskAssignSchedule;

global HardSoftLongScoreHolder scoreHolder;

// Setup
rule "period"
    salience 4
    when
        TaskAssignSchedule($periodFrom : periodFrom, $periodTo : periodTo)
    then
        for (int i = $periodFrom - 1; i <= $periodTo; i++) {
            insert(new PeriodWrapper(i));
        }
end


// Introduce TechnicianPeriod to score efficiently
rule "BootstrapTechnicianPeriod"
    salience 3
    when
        TaskAssignSchedule($periodFrom : periodFrom, $periodTo : periodTo)
        PeriodWrapper(period == $periodFrom - 1);
        $technician : Technician()
    then
        insertLogical(new TechnicianPeriod($technician, $periodFrom - 1, java.util.Collections.EMPTY_LIST, null));
end


rule "TechnicianPeriod"
    salience 2
    when
        PeriodWrapper($period : period);
        $technician : Technician()
        $taskAssignments : ArrayList() from collect (
            TaskAssignment(technician == $technician, start == $period)
        )
        $prevTechPeriod : TechnicianPeriod(technician == $technician, period == $period - 1)
    then
        insertLogical(new TechnicianPeriod($technician, $period, $taskAssignments, $prevTechPeriod));
end

// ############################################################################
// Hard constraints
// ############################################################################

rule "skill"
    when
        TaskAssignment(matchingSkill == false)
    then
        scoreHolder.addHardConstraintMatch(kcontext, -1);
end

rule "token"
    when
        TaskAssignment(matchingToken == false)
    then
        scoreHolder.addHardConstraintMatch(kcontext, -1);
end

rule "technicianCapacity"
    when
        TechnicianPeriod($capacityRemaining: capacityRemaining < 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, $capacityRemaining);
end

rule "technicianLocation"
    when
        TechnicianPeriod($noOfTaskLocations : noOfTaskLocations > 1)
    then
        scoreHolder.addHardConstraintMatch(kcontext, -$noOfTaskLocations + 1);
end

// ############################################################################
// Soft constraints
// ############################################################################

rule "onTimeStartDeviation"
    when
        TaskAssignSchedule($optControlParameters : optControlParameters)
        accumulate(
            $taskAssignment : TaskAssignment($task: task, startDeviation < 0);
            $onTimeStartDeviation : sum($taskAssignment.getFactoredStartDeviation($optControlParameters.getTaskPriorityFactor($task.priority)))
         )
    then
        scoreHolder.addSoftConstraintMatch(kcontext, $optControlParameters.onTimeStart * $onTimeStartDeviation);
end

rule "onTimeEndDeviation"
    when
        TaskAssignSchedule($optControlParameters : optControlParameters, $durationsPerPeriod : durationsPerPeriod)
        accumulate(
            $taskAssignment : TaskAssignment($task: task, endDeviation < 0);
            $onTimeEndDeviation : sum($taskAssignment.getFactoredEndDeviation($optControlParameters.getTaskPriorityFactor($task.priority)))
         )
    then
        scoreHolder.addSoftConstraintMatch(kcontext, $optControlParameters.onTimeComplete * $onTimeEndDeviation);
end

rule "technicianUtilization"
    when
        TaskAssignSchedule($optControlParameters : optControlParameters)
        $technician : Technician($totalCapacity : totalCapacity)
        accumulate(
            $taskAssignment : TaskAssignment(technician == $technician, $duration : duration);
            $capacityUsed : sum($duration)
         )
    then
        scoreHolder.addSoftConstraintMatch(kcontext,
                        $optControlParameters.technicianUtilization * Math.round(10 * $capacityUsed / $totalCapacity));
end

rule "technicianRelocation"
    when
        TaskAssignSchedule($optControlParameters : optControlParameters)
        TechnicianPeriod($location: location, $technician : technician, $period : period)
        exists TechnicianPeriod(technician == $technician, period == $period + 1, location != $location, noOfTaskLocations == 1)
        accumulate(
            TechnicianPeriod(technician == $technician, period == $period + 1, location != $location, noOfTaskLocations == 1);
            $relocations : count(1)
         )
    then
        scoreHolder.addSoftConstraintMatch(kcontext, $optControlParameters.technicianRelocation * $relocations);
end